//@version=6
// インジケーター名: 「EMA + 一目均衡表 + BB（ストラテジー）」
// 
// 概要:
// 本インジケーターは、複数の指数移動平均（EMA）とローソク足パターン「包み線」を用いた売買シグナルを提供します。
// 
// 主な機能:
// - 5本のEMA（5, 25, 50, 100, 200）をプロット
// - EMA5とEMA25のクロスによる売買シグナルを表示
//   ・買い：EMA5が25を上抜け ＋ 価格が雲の下
//   ・売り：EMA5が25を下抜け ＋ 価格が雲の上
// - ボリンジャーバンドを表示（中心線、上下バンド）
// - 出来高が急騰したときにシグナル（●）を表示
// - 一目均衡表を表示

strategy('EMA + 一目均衡表 + BB（ストラテジー）', overlay = true, initial_capital = 1000000, default_qty_type = strategy .percent_of_equity, default_qty_value = 20)

// ===== library import =====
import ise0615/EMA_Ichimoku_BB_Function/2 as lib

// パラメーター設定
src = input.source(close, "元データ")

// EMA期間設定
grp_ema = "EMA 設定"
ema_len1 = input.int(5,   "EMA 1", minval=1, group=grp_ema)
ema_len2 = input.int(25,  "EMA 2", minval=1, group=grp_ema)
ema_len3 = input.int(50,  "EMA 3", minval=1, group=grp_ema)
ema_len4 = input.int(100, "EMA 4", minval=1, group=grp_ema)
ema_len5 = input.int(200, "EMA 5", minval=1, group=grp_ema)

// ボリンジャーバンド設定
grp_bb = "ボリンジャーバンド 設定"
bb_len  = input.int(20,    "BB 期間", minval=1, group=grp_bb)
bb_mult = input.float(2.0, "BB 標準偏差", minval=0.1, step=0.1, group=grp_bb)

// 一目均衡表設定
grp_ichi = "一目均衡表 設定"
ichi_tenkan = input.int(9,  "転換線 期間", minval=1, group=grp_ichi)
ichi_kijun  = input.int(26, "基準線 期間", minval=1, group=grp_ichi)
ichi_senkou = input.int(52, "先行スパンB 期間", minval=1, group=grp_ichi)
ichi_disp   = input.int(26, "先行スパン変位 (Offset)", minval=1, group=grp_ichi)

// 決済設定
grp_sl = "損切設定"
sl_buffer = input.float(0.02,  "損切のバッファ", minval=0, step=0.01, group=grp_sl)

// ライブラリ計算の実行
res = lib.calc(src, ema_len1, ema_len2, ema_len3, ema_len4, ema_len5, bb_len, bb_mult, ichi_tenkan, ichi_kijun, ichi_senkou, ichi_disp)

// EMA
plot(res.ema100, color = color.orange,    title = 'ema100', linewidth = 2)
plot(res.ema200, color = color.red, title = 'ema200', linewidth = 2)

// シグナル表示
plotshape(res.emaBuy,  title = 'EMAクロス買い', location = location.belowbar, color = color.lime, style = shape.labelup, text = '買', textcolor = color.black)
plotshape(res.emaSell, title = 'EMAクロス売り', location = location.abovebar, color = color.red, style = shape.labeldown, text = '売', textcolor = color.white)

// ==============================
// ストラテジー変数と設定
// ==============================
var float longStopPrice = na
var float shortStopPrice = na
ema5  = ta.ema(src, ema_len1)
ema25 = ta.ema(src, ema_len2)

// ==============================
// エントリーロジック
// ==============================

// 買いシグナル
if res.emaBuy
    // 損切：安値(low)からさらに2%下
    longStopPrice := low * (1 - sl_buffer) 
    // 成行でエントリー
    strategy.entry("Long", strategy.long) 

// 売りシグナル
if res.emaSell
    // 損切：高値(high)からさらに2%上
    shortStopPrice := high * (1 + sl_buffer)
    // 成行でエントリー
    strategy.entry("Short", strategy.short) 

// ==============================
// 決済ロジック
// ==============================

// --- 買いポジション ---
if strategy.position_size > 0
    // 利確1：EMAの逆クロス (EMA5が25を下抜け)
    exit_ema_cross = ta.crossunder(ema5, ema25)
    // 利確2：反対の売りサイン(res.emaSell)が点灯した場合
    exit_signal = res.emaSell
    
    if exit_ema_cross or exit_signal
        strategy.close("Long", comment = exit_signal ? "TP Signal" : "TP Cross")
    
    // 損切：全戻し-バッファ
    strategy.exit("Long SL", "Long", stop = longStopPrice, comment="SL Full")

// --- 売りポジション ---
if strategy.position_size < 0
    // 利確1：EMAの逆クロス (EMA5が25を上抜け)
    exit_ema_cross = ta.crossover(ema5, ema25)
    // 利確2：反対の買いサイン(res.emaBuy)が点灯した場合
    exit_signal = res.emaBuy
    
    if exit_ema_cross or exit_signal
        strategy.close("Short", comment = exit_signal ? "TP Signal" : "TP Cross")
    
    // 損切：全戻し+バッファ
    strategy.exit("Short SL", "Short", stop = shortStopPrice, comment="SL Full")

// 損切ラインの描画
plot(strategy.position_size > 0 ? longStopPrice : na, color=color.red, style=plot.style_linebr, linewidth=2, title="Long SL (Full-2%)")
plot(strategy.position_size < 0 ? shortStopPrice : na, color=color.orange, style=plot.style_linebr, linewidth=2, title="Short SL (Full+2%)")
