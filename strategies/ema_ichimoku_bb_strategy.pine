//@version=6
strategy('EMA + 一目均衡表 + BB ストラテジー', overlay = true, initial_capital = 1000000, default_qty_type = strategy.percent_of_equity, default_qty_value = 10)

// ===== library import =====
import ise0615/CommonFunctions/2 as cf

// === パラメーター設定 ===
gp_ema = "EMA設定"
src = input.source(close, title = '元データ', group = gp_ema)
emaLength5   = input.int(5, title = 'ema5期間', group = gp_ema)
emaLength25  = input.int(25, title = 'ema25期間', group = gp_ema)
emaLength100 = input.int(100, title = 'ema100期間', group = gp_ema)

gp_ichimoku = "一目均衡表パラメータ"
tenkanLen = input.int(9, title = '転換線期間', group = gp_ichimoku)
kijunLen = input.int(26, title = '基準線期間', group = gp_ichimoku)
senkouSpanB_Len = input.int(52, title = '先行スパンB期間', group = gp_ichimoku)
displacement = input.int(26, title = '表示ずらし', group = gp_ichimoku)

// === インジケーター計算 ===
ema5   = cf.ema(src, emaLength5)
ema25  = cf.ema(src, emaLength25)
ema100 = cf.ema(src, emaLength100)

[tenkan, kijun, senkouA, senkouB] = cf.ichimokuBasic(tenkanLen, kijunLen, senkouSpanB_Len)

// --- 条件判定 ---
// 1. 雲の位置判定（先行スパンはdisplacement分前に描画されているため、現在の価格と比較）
inBullishCloud = close > senkouA[displacement] and close > senkouB[displacement]
inBearishCloud = close < senkouA[displacement] and close < senkouB[displacement]

// 2. EMA100のトレンド判定
ema100IsRising  = ta.rising(ema100, 1) 
ema100IsFalling = ta.falling(ema100, 1)

// 3. クロス＋雲の条件（トリガーの準備）
emaCrossUpTrigger   = cf.emaCrossUp(src, emaLength5, emaLength25) and inBearishCloud
emaCrossDownTrigger = cf.emaCrossDown(src, emaLength5, emaLength25) and inBullishCloud

// --- 状態管理（クロス発生からサインが出るまで維持） ---
var bool isWaitingForBuy  = false
if emaCrossUpTrigger
    isWaitingForBuy := true
if strategy.position_size > 0 or emaCrossDownTrigger // エントリー後、または逆クロスでリセット
    isWaitingForBuy := false

var bool isWaitingForSell = false
if emaCrossDownTrigger
    isWaitingForSell := true
if strategy.position_size < 0 or emaCrossUpTrigger // エントリー後、または逆クロスでリセット
    isWaitingForSell := false

// 4. 包み線の判定
bullishEngulfing = cf.bullishEngulfing()
bearishEngulfing = cf.bearishEngulfing()

// === エントリー条件 ===
longCondition  = isWaitingForBuy  and bullishEngulfing and ema100IsRising
shortCondition = isWaitingForSell and bearishEngulfing and ema100IsFalling

// === エントリー実行 ===
if longCondition
    strategy.entry("Long", strategy.long, comment="EMA_Cross_Buy")

if shortCondition
    strategy.entry("Short", strategy.short, comment="EMA_Cross_Sell")

// === 決済条件 (例: 逆のクロスが発生したら決済) ===
if ta.crossunder(ema5, ema25)
    strategy.close("Long", comment="Exit_Long")

if ta.crossover(ema5, ema25)
    strategy.close("Short", comment="Exit_Short")

// === チャート描画 (確認用) ===
plot(ema100, color = color.orange, title = 'ema100', linewidth = 2)
p_senkouA = plot(senkouA, offset = displacement, color = color.teal, title = '先行スパン1')
p_senkouB = plot(senkouB, offset = displacement, color = color.maroon, title = '先行スパン2')
fill(p_senkouA, p_senkouB, color = senkouA > senkouB ? color.new(color.green, 90) : color.new(color.red, 90))
